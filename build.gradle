plugins {
    id 'java'
    id "net.ltgt.apt-idea" version "0.18"
    id "com.jfrog.artifactory" version "4.7.5"
    id 'maven-publish'
}

group 'com.foxpify'
version '1.1.2-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.vertx:vertx-core:3.5.4'
    compile 'io.vertx:vertx-sql-common:3.5.4'

    compile 'com.dslplatform:dsl-json-java8:1.8.2'
    annotationProcessor 'com.dslplatform:dsl-json-java8:1.8.2'
    
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-slf4j18-impl', version: '2.11.1'
    testCompile 'io.vertx:vertx-mysql-postgresql-client:3.5.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.allSource
    classifier = 'sources'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact (sourcesJar) {
                classifier = 'sources'
            }
        }
    }
}

def publishRepoKey = project.version.endsWith('-SNAPSHOT') ? 'libs-snapshot' : 'libs-release'

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = "${publishRepoKey}-local"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team' : 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = "$publishRepoKey"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
    }
}